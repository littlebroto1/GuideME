
plugins {
    id 'java-library'
    id 'maven-publish'
}

group = "guideme.libs"
version = "${markdown_version}"



//apply plugin: ProjectDefaultsPlugin

repositories {
    mavenCentral()
}

configure([tasks.compileJava]) {
    sourceCompatibility = 16 // for the IDE support
    options.release = 8

    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

dependencies {
    annotationProcessor 'com.github.bsideup.jabel:jabel-javac-plugin:0.4.2'
    compileOnly 'com.github.bsideup.jabel:jabel-javac-plugin:0.4.2'

    implementation 'org.slf4j:slf4j-api:1.7.36'

    compileOnly 'org.jetbrains:annotations:23.0.0'
    implementation 'com.google.code.gson:gson:2.10'

    testCompileOnly 'org.jetbrains:annotations:23.0.0'
    testImplementation platform("org.junit:junit-bom:${junit_version}")
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testRuntimeOnly "org.slf4j:slf4j-simple:1.7.36"
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

jar {
    manifest {
        attributes('FMLModType': 'LIBRARY')
    }
}

test {
    useJUnitPlatform()
}

//////////////////
// Maven publish
publishing {
    publications {
        maven(MavenPublication) {
            artifactId = "markdown"
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}
